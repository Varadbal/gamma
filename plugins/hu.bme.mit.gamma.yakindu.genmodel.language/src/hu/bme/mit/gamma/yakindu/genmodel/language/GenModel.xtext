/********************************************************************************
 * Copyright (c) 2018 Contributors to the Gamma project
 *
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * SPDX-License-Identifier: EPL-1.0
 ********************************************************************************/
grammar hu.bme.mit.gamma.yakindu.genmodel.language.GenModel with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.mit.bme.hu/gamma/statechart/Model" as model
import "platform:/resource/hu.bme.mit.gamma.statechart.model/model/statechart.ecore#//interface" as interface
import "http://www.mit.bme.hu/gamma/yakindu/Genmodel/"

import "http://www.yakindu.org/sct/sgraph/2.0.0" as sgraph
import "http://www.yakindu.org/sct/statechart/SText" as stext
import "http://www.yakindu.org/base/types/2.0.0" as types

GenModel returns GenModel:
	('import' 
		(packageImports+=[model::Package | STRING] |
		'external' statechartImports+=[sgraph::Statechart| STRING])
	)*
	(tasks += Task)*
;

Task returns Task:
	YakinduCompilation | CodeGeneration | AnalysisModelTransformation
;

YakinduCompilation returns YakinduCompilation:
	StatechartCompilation | InterfaceCompilation
;

InterfaceCompilation returns InterfaceCompilation:
	'interface' statechart=[sgraph::Statechart] '{'
		(
			'fileName' '=' fileName=STRING | // The name of the generated file
			'projectName' '=' targetProject=STRING | // The name of the project where the Gamma file is generated
			'folderName' '=' targetFolder=STRING | // The location of the folder relative to the project
			'packageName' '=' packageName=STRING // Gamma package name
		)*
	'}'
;

StatechartCompilation returns StatechartCompilation:
	'statechart' statechart=[sgraph::Statechart] '{'
		(
			'fileName' '=' fileName=STRING | // The name of the generated file
			'projectName' '=' targetProject=STRING | // The name of the project where the Gamma file is generated
			'folderName' '=' targetFolder=STRING | // The location of the folder relative to the project
			'statechartName' '=' statechartName=STRING | // Gamma statechart name
			'packageName' '=' packageName=STRING // Gamma package name
		)*
		'mappings' '{'
			(interfaceMappings+=InterfaceMapping)*
		'}'
	'}'
;

CodeGeneration returns CodeGeneration:
	'code' '{'
		(
			'package' '=' packageName=STRING &
			'project' '=' targetProject=STRING &
			'folder' '=' targetFolder=STRING
		)
		'language' '=' language=ProgrammingLanguage
	'}'
;

enum ProgrammingLanguage returns ProgrammingLanguage:
	JAVA = 'java'
;

AnalysisModelTransformation:
	'analysis' '{'
		(
			'name' '=' fileName=STRING &
			'project' '=' targetProject=STRING &
			'folder' '=' targetFolder=STRING &			
			'language' '=' language=AnalysisLanguage &
			'component' '=' component=[model::Component]
		)
	'}'
;

enum AnalysisLanguage returns AnalysisLanguage:
	UPPAAL = "UPPAAL"
;

InterfaceMapping  returns InterfaceMapping:
	'map' yakinduInterface=[stext::InterfaceScope] '=' gammaInterface=[interface::Interface] 'as' realizationMode=RealizationMode ('{'
		eventMappings+=EventMapping*
	'}')?
;

EventMapping returns EventMapping:
	'map' yakinduEvent=[types::Event] '=' gammaEvent=[interface::Event] 
;

enum RealizationMode returns model::RealizationMode:
	PROVIDED = 'provided' | REQUIRED = 'required'
;
